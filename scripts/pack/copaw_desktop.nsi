; XiaoshuClaw NSIS installer. Run makensis from repo root after
; building dist/win-unpacked (see scripts/pack/build_win.ps1).
; Usage: makensis /DCOPAW_VERSION=1.2.3 /DOUTPUT_EXE=dist\XiaoshuClaw-Setup-1.2.3.exe scripts\pack\copaw_desktop.nsi

!include "MUI2.nsh"
!define MUI_ABORTWARNING
; Use custom icon from unpacked env (copied by build_win.ps1)
!define MUI_ICON "${UNPACKED}\icon.ico"
!define MUI_UNICON "${UNPACKED}\icon.ico"

!ifndef COPAW_VERSION
  !define COPAW_VERSION "0.0.0"
!endif
!ifndef OUTPUT_EXE
  !define OUTPUT_EXE "dist\XiaoshuClaw-Setup-${COPAW_VERSION}.exe"
!endif

Name "XiaoshuClaw"
OutFile "${OUTPUT_EXE}"
InstallDir "$LOCALAPPDATA\XiaoshuClaw"
InstallDirRegKey HKCU "Software\XiaoshuClaw" "InstallPath"
RequestExecutionLevel user

!insertmacro MUI_PAGE_DIRECTORY
!insertmacro MUI_PAGE_INSTFILES
!insertmacro MUI_PAGE_FINISH
!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES
!insertmacro MUI_LANGUAGE "SimpChinese"

; Pass /DUNPACKED=full_path from build_win.ps1 so path works when cwd != repo root
!ifndef UNPACKED
  !define UNPACKED "dist\win-unpacked"
!endif

Section "XiaoshuClaw" SEC01
  SetOutPath "$INSTDIR"
  File /r "${UNPACKED}\*.*"
  WriteRegStr HKCU "Software\XiaoshuClaw" "InstallPath" "$INSTDIR"
  WriteUninstaller "$INSTDIR\Uninstall.exe"

  ; Main shortcut - uses VBS to hide console window
  CreateShortcut "$SMPROGRAMS\XiaoshuClaw.lnk" "$INSTDIR\XiaoshuClaw.vbs" "" "$INSTDIR\icon.ico" 0
  CreateShortcut "$DESKTOP\XiaoshuClaw.lnk" "$INSTDIR\XiaoshuClaw.vbs" "" "$INSTDIR\icon.ico" 0
  
  ; Debug shortcut - shows console window for troubleshooting
  CreateShortcut "$SMPROGRAMS\XiaoshuClaw (Debug).lnk" "$INSTDIR\XiaoshuClaw (Debug).bat" "" "$INSTDIR\icon.ico" 0
SectionEnd

Section "Uninstall"
  Delete "$SMPROGRAMS\XiaoshuClaw.lnk"
  Delete "$SMPROGRAMS\XiaoshuClaw (Debug).lnk"
  Delete "$DESKTOP\XiaoshuClaw.lnk"
  RMDir /r "$INSTDIR"
  DeleteRegKey HKCU "Software\XiaoshuClaw"
SectionEnd
